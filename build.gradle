// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.google.com' }
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.google.guava:guava:24.1-jre'
        classpath 'com.android.tools.build:gradle:3.4.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
        google()
    }
}

wrapper {
    gradleVersion = '4.6'
}


ext {
    supportLibraryVersion = '28.0.0'
    timberLibraryVersion = '4.6.0'
    butterknifeLibraryVersion = '8.8.1'
    gmsAdsLibraryVersion = '16.0.0'
    googleApiClientVersion = '1.23.0'
    espressoLibraryVersion = '3.0.1'
    constraintLayoutVersion = '1.0.2'
    testRunnerVersion = '1.0.1'
    endpointsGradlePluginVersion = '1.0.3'
    appengineGradlePluginVersion = '1.3.3'
    endpointsFrameworkVersion = '2.0.9'
    junitVersion = '4.12'
    findbugsVersion = '3.0.1'
    butterknifeLibraryVersion = '8.8.1'
}


// Custom task to start engine, run test and finally stop engine

task startAppEngine (dependsOn: ":backend:appengineStart") {
    doLast {
        println "Task [startAppEngine] completed >> App Engine started"
    }
}

task runAndroidTest (dependsOn: ":app:connectedAndroidTest") {
    doLast {
        println "Task [runAndroidTest] completed >> Test reports can be found at project-folder/app/build/reports directory"
    }
}

task stopAppEngine (dependsOn: ":backend:appengineStop") {
    doLast {
        println "Task [stopAppEngine] completed >> App Engine stopped"
    }
}

task jokeAppCustomTask (dependsOn: "startAppEngine") {
    dependsOn "runAndroidTest"
    mustRunAfter "startAppEngine"
    doLast {
        println "Task [jokeAppCustomTask] completed >> Task cycle completed"
    }
    finalizedBy "stopAppEngine"
}

runAndroidTest.mustRunAfter "startAppEngine"
runAndroidTest.finalizedBy "stopAppEngine"